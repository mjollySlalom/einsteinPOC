/*
    Matt Jolly | Slalom Consulting | 6/16/17
    Description - Wrapper class for predictions generated by the Recommendation Engine 
*/
public class PredictionWrapper implements Comparable{
    public Id itemId{get;set;}
    public Decimal score{get;set;}
    public Product2 item{get;set;}
    
    //Overload constructor to support building in multiple ways
    public PredictionWrapper(String itId, Decimal scr) {
        itemId = itId;
        score = scr;
    }
    
    public PredictionWrapper(String itId, String scr) {
        itemId = itId;
        score = Decimal.valueOf(scr);
    }
    
    public PredictionWrapper(OpportunityLineItem oli) {
        itemId = oli.Product2.Id;
        score = 1.0;
        item = new Product2(Id = oli.Product2.Id, Name = oli.Product2.Name);
    }
    
    //Allow wrappers to be sorted by similarity score (descending)
    public Integer compareTo(Object compareTo) {
        PredictionWrapper compareToPrediction = (PredictionWrapper) compareTo;
        if(score > compareToPrediction.score) {
            return -1;
        } else if(score < compareToPrediction.score) {
            return 1;
        } else {
            return 0;
        }
    }
}